#compdef dotfiles

typeset -A opt_args
setopt extendedglob

_dotfiles_opts_commands=(
  {-I,--install}"[Install]"
  {-T,--test}"[Test]"
  {-U,--uninstall}"[Uninstall]"

  {-h,--help}"[Display usage]"
)

_dotfiles_opts_common=(
  "-g[Configure GUI environment]"
)

_dotfiles_opts_install=(
  "-p[Install system packages]"
  "-s[Install systemd units]"
)

_dotfiles_action_install() {
  _arguments -s : \
    "(--install -I)"{-I,--install} \
    "$_dotfiles_opts_install[@]" \
    "$_dotfiles_opts_common[@]"
}

_dotfiles_action_test() {
  _arguments -s : \
    "(--test -T)"{-T,--test}
}

_dotfiles_action_uninstall() {
  _arguments -s : \
    "(--uninstall -U)"{-U,--uninstall} \
    "$_dotfiles_opts_common[@]"
}

local -a args cmds;
local tmp
args=( ${${(M)words:#-*}#-} )
for tmp in $words
do
  cmds+=("${${_dotfiles_opts_commands[(r)*$tmp\[*]%%\[*}#*\)}")
done

case $args in
  h)
    return 0
    ;;
  I*)
    _dotfiles_action_install
    ;;
  T*)
    _dotfiles_action_test
    ;;
  U*)
    _dotfiles_action_uninstall
    ;;
  *)
    case ${(M)words:#--*} in
      *--help*)
        return 0
        ;;
      *--install*)
        _dotfiles_action_install
        ;;
      *--test*)
        _dotfiles_action_test
        ;;
      *--uninstall*)
        _dotfiles_action_uninstall
        ;;
      *)
        _arguments -s : \
          "$_dotfiles_opts_commands[@]"
        ;;
    esac
esac
